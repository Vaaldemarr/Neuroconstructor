&НаКлиенте
Процедура СообщитьОбОшибке(Поле, Текст)
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = Текст;
	Сообщение.Поле = Поле;
	Сообщение.Сообщить();
КонецПроцедуры

&НаКлиенте
Процедура Заполнить(Команда)
	Если Объект.ТекстПрограммы = "" Тогда
		СообщитьОбОшибке("Объект.ТекстПрограммы", "Отсутствует текст программы алгоритма!");
		Возврат
	КонецЕсли;
	
	Если Объект.Параметры.Количество()>0 Тогда
		Режим = РежимДиалогаВопрос.ДаНет;
		Оповещение = Новый ОписаниеОповещения("ПослеЗакрытияВопросаОчиститьПараметры", ЭтотОбъект, Параметры);
		ПоказатьВопрос(Оповещение, "Очистить параметры?", Режим, 0);
	Иначе
		ПоискОбъявленияВТекстеПрограммы()
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗакрытияВопросаОчиститьПараметры(Результат, Параметры) Экспорт
	Если Результат = КодВозвратаДиалога.Да Тогда
		Объект.Параметры.Очистить();
		Модифицированность=Истина;
	КонецЕсли;
	
	ПоискОбъявленияВТекстеПрограммы()
КонецПроцедуры

&НаКлиенте
Процедура ПоискОбъявленияВТекстеПрограммы()
	НайденоОбъявление = Ложь; НайденРезультат = Ложь;
	Строки = СтрРазделить(Объект.ТекстПрограммы, Символы.ПС, Ложь);
	Для Каждого Строка Из Строки Цикл
		Если НайденоОбъявление Тогда
			Если Нрег(Лев(СокрЛ(Строка), 7))="return " Тогда
				НайденРезультат=Истина; Прервать
			КонецЕсли;
		ИначеЕсли Нрег(Лев(Строка, 4))="def " Тогда
			ПозСкобка1=СтрНайти(Строка,"(",НаправлениеПоиска.СНачала, 5);
			Если ПозСкобка1>0 Тогда
				Идентификатор = Сред(Строка, 5, ПозСкобка1-5);
				ПозСкобка2=СтрНайти(Строка,")",НаправлениеПоиска.СКонца);
				Если ПозСкобка2>ПозСкобка1 Тогда
					Объект.Идентификатор = Идентификатор;
					Модифицированность=Истина;
					СтрокаПараметры = Сред(Строка, ПозСкобка1+1, ПозСкобка2-ПозСкобка1-1);
					СтрокиПараметры = СтрРазделить(СтрокаПараметры,",",Ложь);
					Для Каждого СтрокаПараметр Из СтрокиПараметры Цикл
						ПозРавно=СтрНайти(СтрокаПараметр,"=");
						Если ПозРавно=0 Тогда
							ИмяПараметра = СтрокаПараметр
						Иначе
							ИмяПараметра = Лев(СтрокаПараметр, ПозРавно-1);
						КонецЕсли;
						СтрокаТЧ = Объект.Параметры.Добавить();
						СтрокаТЧ.Имя = СокрЛП(ИмяПараметра);
					КонецЦикла;
					НайденоОбъявление=Истина;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Если НЕ НайденоОбъявление Тогда
		СообщитьОбОшибке("Объект.ТекстПрограммы", "Объявление функции не найдено в тексте программы!");
	Иначе
		Объект.ВозвращаетРезультат = НайденРезультат;
	КонецЕсли;
КонецПроцедуры
