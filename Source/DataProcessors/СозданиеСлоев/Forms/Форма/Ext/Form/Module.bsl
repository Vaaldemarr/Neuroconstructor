
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	СоздатьТаблицуЗаполнения();
КонецПроцедуры

&НаСервере
Процедура СоздатьТаблицуЗаполнения()
	ТаблицаЗаполнения.Очистить();
	Для Каждого ВидСлоя Из Перечисления.Нейро_ВидыСлоев Цикл
		СписокСлоев = Нейро_ОбщийМодуль.ПолучитьТипыСлояПоВидуСлоя(ВидСлоя);
		Для Каждого Элемент Из СписокСлоев Цикл
			СтрокаТЗ = ТаблицаЗаполнения.Добавить();
			СтрокаТЗ.ВидСлоя = ВидСлоя;
			СтрокаТЗ.ИмяГруппы = ВидСлоя;
			СтрокаТЗ.ТипСлоя = Элемент.Значение;
			СтрокаТЗ.НаименованиеСправочника = Элемент.Значение;
			СтрокаТЗ.ИдОбъекта = Нейро_ОбщийМодуль.ПолучитьКомментарийИзЗначенияПеречисления("Нейро_ТипыСлоев", Элемент.Значение);
			СтрокаТЗ.ОписаниеСсылка = Нейро_ОбщийМодуль.ПолучитьСсылкуНаОписаниеСлоя(Элемент.Значение);
			СтрокаТЗ.ОписаниеСлояАнг = СокрЛП(Элемент.Значение)+".htm";
			СтрокаТЗ.ОписаниеСлояРус = СокрЛП(Элемент.Значение)+".htm";
		КонецЦикла;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура ПутьКФайламОписанийНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	Режим = РежимДиалогаВыбораФайла.ВыборКаталога;
	ДиалогОткрытияФайла = Новый ДиалогВыбораФайла(Режим);
	Файл = Новый Файл(ПутьКФайламОписанийАнг);
	ДиалогОткрытияФайла.Каталог=Файл.Путь;
	ДиалогОткрытияФайла.Заголовок = "Укажите путь к файлам описаний (анг)";	
	ДиалогОткрытияФайла.МножественныйВыбор = Ложь;
	ОпОп = Новый ОписаниеОповещения("ПослеЗакрытияДиалогаВыбораКаталогаАнг", ЭтотОбъект);
	ДиалогОткрытияФайла.Показать(ОпОп);
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗакрытияДиалогаВыбораКаталогаАнг(ВыбранныеФайлы, ДополнительныеПараметры) Экспорт
	Если ВыбранныеФайлы=Неопределено Тогда Возврат КонецЕсли;
	ПутьКФайламОписанийАнг = ВыбранныеФайлы[0];
КонецПроцедуры

&НаКлиенте
Процедура ПутьКФайламОписанийРусНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	Режим = РежимДиалогаВыбораФайла.ВыборКаталога;
	ДиалогОткрытияФайла = Новый ДиалогВыбораФайла(Режим);
	Файл = Новый Файл(ПутьКФайламОписанийРус);
	ДиалогОткрытияФайла.Каталог=Файл.Путь;
	ДиалогОткрытияФайла.Заголовок = "Укажите путь к файлам описаний (рус)";	
	ДиалогОткрытияФайла.МножественныйВыбор = Ложь;
	ОпОп = Новый ОписаниеОповещения("ПослеЗакрытияДиалогаВыбораКаталогаРус", ЭтотОбъект);
	ДиалогОткрытияФайла.Показать(ОпОп);
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗакрытияДиалогаВыбораКаталогаРус(ВыбранныеФайлы, ДополнительныеПараметры) Экспорт
	Если ВыбранныеФайлы=Неопределено Тогда Возврат КонецЕсли;
	ПутьКФайламОписанийРус = ВыбранныеФайлы[0];
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьТаблицуЗначениямиПоУмолчанию(Команда)
	СоздатьТаблицуЗаполнения();
КонецПроцедуры

&НаКлиенте
Процедура СоздатьСправочники(Команда)
	МассивСсылок = СоздатьСправочникиНаСервере();
	КаталогАнг = СокрЛП(ПутьКФайламОписанийАнг);
	Если Прав(КаталогАнг,1)<>"\" Тогда КаталогАнг=КаталогАнг+"\" КонецЕсли;
	КаталогРус = СокрЛП(ПутьКФайламОписанийРус);
	Если Прав(КаталогРус,1)<>"\" Тогда КаталогРус=КаталогРус+"\" КонецЕсли;
	Для Каждого СтруктураСсылки Из МассивСсылок Цикл
		ОписаниеСлояАнг="";
		Если СтруктураСсылки.Анг<>"" Тогда
			ЧтениеТекста = Новый ЧтениеТекста(КаталогАнг+СтруктураСсылки.Анг);
			ОписаниеСлояАнг = ЧтениеТекста.Прочитать();
			ЧтениеТекста.Закрыть();
		КонецЕсли;
		ОписаниеСлояРус="";
		Если СтруктураСсылки.Рус<>"" Тогда
			ЧтениеТекста = Новый ЧтениеТекста(КаталогРус+СтруктураСсылки.Рус);
			ОписаниеСлояРус = ЧтениеТекста.Прочитать();
			ЧтениеТекста.Закрыть();
		КонецЕсли;
		ЗаписатьОписаниеНаСервере(ОписаниеСлояАнг, ОписаниеСлояРус, СтруктураСсылки.Ссылка);
	КонецЦикла;
КонецПроцедуры

&НаСервере
Процедура ЗаписатьОписаниеНаСервере(Анг, Рус, СлоиСсылка)
	СлоиОбъект = СлоиСсылка.ПолучитьОбъект();
	СлоиОбъект.ОписаниеСлояАнг = Анг;
	СлоиОбъект.ОписаниеСлояРус = Рус;
	СлоиОбъект.Записать();
КонецПроцедуры

&НаСервере
Функция СоздатьСправочникиНаСервере()
	ЗапросГруппы = Новый Запрос;
	ЗапросГруппы.Текст = 
	"ВЫБРАТЬ
	|	Нейро_Слои.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.Нейро_Слои КАК Нейро_Слои
	|ГДЕ
	|	Нейро_Слои.Наименование = &Наименование
	|	И Нейро_Слои.ЭтоГруппа = ИСТИНА
	|	И Нейро_Слои.ПометкаУдаления = ЛОЖЬ";
	
	Результат = Новый Массив;
	
	Для Каждого СтрокаТЗ Из ТаблицаЗаполнения Цикл
		ЗапросГруппы.УстановитьПараметр("Наименование", СтрокаТЗ.ИмяГруппы);
		Выборка = ЗапросГруппы.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			ГруппаСсылка = Выборка.Ссылка;
		Иначе
			ГруппаОбъект = Справочники.Нейро_Слои.СоздатьГруппу();
			ГруппаОбъект.Наименование = СтрокаТЗ.ИмяГруппы;
			ГруппаОбъект.Записать();
			ГруппаСсылка = ГруппаОбъект.Ссылка;
		КонецЕсли;
		НовыйСлой = Справочники.Нейро_Слои.СоздатьЭлемент();
		НовыйСлой.Родитель = ГруппаСсылка;
		НовыйСлой.Наименование = СтрокаТЗ.НаименованиеСправочника;
		НовыйСлой.ВидСлоя = СтрокаТЗ.ВидСлоя;
		НовыйСлой.ТипСлоя = СтрокаТЗ.ТипСлоя;
		НовыйСлой.ИдОбъекта = СтрокаТЗ.ИдОбъекта;
		НовыйСлой.ОписаниеСсылка = СтрокаТЗ.ОписаниеСсылка;
		НовыйСлой.Записать();
		Результат.Добавить(Новый Структура("Ссылка, Анг, Рус", НовыйСлой.Ссылка,СтрокаТЗ.ОписаниеСлояАнг,СтрокаТЗ.ОписаниеСлояРус));
	КонецЦикла;
	Возврат Результат;
КонецФункции
